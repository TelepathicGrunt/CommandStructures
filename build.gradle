buildscript {
	repositories {
		maven { url "https://plugins.gradle.org/m2/" }
	}
	dependencies {
		classpath group: 'gradle.plugin.com.modrinth.minotaur', name: 'Minotaur', version: '1.2.1'
	}
}
plugins {
	alias(libs.plugins.quilt.loom)
	id "net.darkhax.curseforgegradle" version "1.0.7"
	id 'maven-publish'
}

version = "${project.mod_version}+${libs.versions.minecraft.get()}"
archivesBaseName = "${project.modid}_quilt"
group = project.maven_group

repositories {}

dependencies {
	minecraft libs.minecraft
	mappings( loom.layered {
		addLayer quiltMappings.mappings("org.quiltmc:quilt-mappings:${libs.versions.quilt.mappings.get()}:v2")
		officialMojangMappings()
	})
	modImplementation libs.quilt.loader
	modImplementation libs.quilted.fabric.api
}

processResources {
	inputs.property "version", project.version

	filesMatching("quilt.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = 'UTF-8'
	it.options.release.set(17)
}

java {
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${archivesBaseName}"}
	}
}

apply from: 'gradle/maven.gradle'
apply from: 'gradle/modrinth.gradle'
task publishCurseForge(type: net.darkhax.curseforgegradle.TaskPublishCurseForge) {
	apiToken = "$System.env.CURSEFORGEKEY"
	int projectId = 565119
	String changelogText = new File("CHANGELOG.md").getText('UTF-8')

	def mainFile = upload(projectId, remapJar)
	disableVersionDetection()
	mainFile.addGameVersion("1.18.2")

	mainFile.changelog = changelogText
	mainFile.changelogType = "markdown"
	mainFile.releaseType = "alpha"
	mainFile.displayName = "CommandStructures-Quilt v${project.mod_version} (${libs.versions.minecraft.get()} MC)"
}

task buildAndRelease {}
tasks.publish.mustRunAfter tasks.build
tasks.publishCurseForge.mustRunAfter tasks.publish
tasks.publishModrinth.mustRunAfter tasks.publish
tasks.publishCurseForge.dependsOn("remapJar")
tasks.buildAndRelease.dependsOn(tasks.build, tasks.publish, tasks.publishCurseForge, tasks.publishModrinth)
