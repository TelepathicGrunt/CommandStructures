import com.modrinth.minotaur.TaskModrinthUpload
plugins {
    id 'java-library'
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id "net.darkhax.curseforgegradle" version "1.0.7"
    id "com.modrinth.minotaur" version "2.+"
    id 'net.neoforged.gradle.userdev' version '7.0.53'
}

apply plugin: "com.modrinth.minotaur"

version = "${mod_version}+${mc_version}"
group = "com.telepathicgrunt"
archivesBaseName = "${modid}_neoforge"

repositories {
    mavenLocal()
}

base {
    archivesName = archivesBaseName
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

minecraft.accessTransformers.file rootProject.file('src/main/resources/META-INF/accesstransformer.cfg')

runs {
	client {
		modSource project.sourceSets.main 
	}

	server {
		modSource project.sourceSets.main
        programArgument '--nogui'
	}
}
	
repositories {
}

dependencies {
    implementation "net.neoforged:neoforge:${neo_version}"
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}

apply from: 'gradle/manifest.gradle'
apply from: 'gradle/maven.gradle'

String changelogText = file("CHANGELOG.md").getText('UTF-8')
task publishCurseForge(type: net.darkhax.curseforgegradle.TaskPublishCurseForge) {
    apiToken = "$System.env.CURSEFORGEKEY"
    int projectId = 565119

    def mainFile = upload(projectId, jar)
    mainFile.changelog = changelogText
    mainFile.changelogType = "markdown"
    mainFile.releaseType = "release"
    mainFile.displayName = "CommandStructures-Neoforge v${project.mod_version} (${project.mc_version} MC)"
    mainFile.addGameVersion("${project.mc_version}")
    mainFile.addGameVersion("NeoForge")
}

modrinth {
    token = "$System.env.MODRINTH"
    projectId = 'WEfvvlnl'
    versionName = "CommandStructures-Neoforge"
    versionNumber = project.version + "_neoforge"
    uploadFile = jar
    versionType = "RELEASE"
    changelog = changelogText
    gameVersions = ["${project.mc_version}"]
    loaders = ['neoforge']
}

task buildAndRelease {}
tasks.publish.mustRunAfter tasks.build
tasks.publishCurseForge.mustRunAfter tasks.publish
tasks.modrinth.mustRunAfter tasks.publish
tasks.buildAndRelease.dependsOn(tasks.build, tasks.publish, tasks.publishCurseForge, tasks.modrinth)
